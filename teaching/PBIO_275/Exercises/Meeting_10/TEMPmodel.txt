
  model {

    # subjects, e.g., individual players
    for ( sIdx in 1:Nsubj ) {
      z[sIdx] ~ dbin( theta[sIdx] , N[sIdx] )
      theta[sIdx] ~ dbeta( omega[c[sIdx]]*(kappa[c[sIdx]]-2)+1 , 
                           (1-omega[c[sIdx]])*(kappa[c[sIdx]]-2)+1 ) 
    }
    
    # categories, e.g., positions
    for ( cIdx in 1:Ncat ) {
      omega[cIdx] ~ dbeta( omegaO*(kappaO-2)+1 , 
                           (1-omegaO)*(kappaO-2)+1 )
      kappa[cIdx] <- kappaMinusTwo[cIdx] + 2
      kappaMinusTwo[cIdx] ~ dgamma( Sk , Rk ) # was 0.01, 0.01 -> mean=1 , sd=10 (generic vague) *** NEW
    }
    omegaO ~ dbeta( 1.0 , 1.0 ) 
    #omegaO ~ dbeta( 1.025 , 1.075 ) # mode=0.25 , concentration=2.1
    kappaO <- kappaMinusTwoO + 2
    kappaMinusTwoO ~ dgamma( 0.01 , 0.01 )  # mean=1 , sd=10 (generic vague)
    Sk ~ dgamma( 0.01 , 0.01 )  # mean=1 , sd=10 (generic vague) *** NEW
    Rk ~ dgamma( 0.01 , 0.01 )  # mean=1 , sd=10 (generic vague) *** NEW

    # Contrasts
    c1Minusc3<-omega[1]-omega[3] # difference in modes of pitcher and 1st base positions
    c1Minusc3Prob<-step(c1Minusc3) # returns a 1 if c1Minusc3 > 0

    p1Minusp2<-theta[1]-theta[2] # difference in batting average of player 1 and player 2
    p1Minusp2Prob<-step(p1Minusp2) # returns a 1 if c1Minusc3 > 0

    #kappaMinusTwoO ~ dgamma( 1.01005 , 0.01005012 )  # mode=1 , sd=100
    #kappaMinusTwoO ~ dgamma( 1.105125 , 0.1051249 )  # mode=1 , sd=10
    #kappaMinusTwoO ~ dgamma( 1.105125 , 0.01051249 )  # mode=10 , sd=100
  }
  
